PREFIX region: <http://sadiframework.org/ontologies/GMOD/BiopolymerRegion.owl#>
PREFIX strand: <http://sadiframework.org/ontologies/GMOD/Strand.owl#>
PREFIX range: <http://sadiframework.org/ontologies/GMOD/RangedSequencePosition.owl#>
PREFIX sio: <http://semanticscience.org/resource/>

SELECT * # ?start_pos ?end_pos ?strand_type ?ref_feature_id_type ?ref_feature_id
WHERE {

    <[% biopolymer_region_uri %]>
        a region:BiopolymerRegion;
        region:position [
            a range:RangedSequencePosition;
            range:in_relation_to ?ref_sequence;
            sio:SIO_000053 [ a range:StartPosition; sio:SIO_000300 ?start_pos ]; # p = 'has proper part'
            sio:SIO_000053 [ a range:EndPosition; sio:SIO_000300 ?end_pos ] ] .  # p = 'has proper part'

   #---------------------------------------------------------------------------------
   # An input BiopolymerRegion may have any number of RangedSequencePositions,
   # describing the position of the region relative to different reference
   # features. 
   #
   # For each RangedSequencePosition, there are two possible cases: 
   #
   # 1) ?ref_sequence is a sequence for the plus or minus strand of some
   #    double-stranded feature (e.g. a chromosome).
   #
   # 2) ?ref_sequence is a sequence for some single-stranded feature 
   #    (e.g. a protein)
   #---------------------------------------------------------------------------------

   # Case 1): Reference feature is double-stranded.

   {
       ?ref_sequence 
            sio:SIO_000011 # p = 'is attribute of' 
                [ a ?strand_type; # strand:PlusStrand or strand:MinusStrand
                  sio:SIO_000093 ?ref_feature ] . # p = 'is proper part of'
   }
    
   UNION

   # Case 2): Reference feature is single-stranded.

   {
        ?ref_sequence
            sio:SIO_000011 ?ref_feature . # p = 'is attribute of'
   }        

   #---------------------------------------------------------------------------------
   # Get the identifer for the reference feature.
   #---------------------------------------------------------------------------------
   
   ?ref_feature sio:SIO_000008 
        [ a ?ref_feature_id_type;
          sio:SIO_000300 ?ref_feature_id ] . # p = 'has value' 

}


